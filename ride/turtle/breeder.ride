{-# STDLIB_VERSION 5 #-}
{-# CONTENT_TYPE DAPP #-}
{-# SCRIPT_TYPE ACCOUNT #-}

func tryGetStringExternal(address: Address, key: String) = {
    match getString(address, key) {
        case a:String => a
        case _ => ""
    }
}

func tryGetString(key: String) = {
    tryGetStringExternal(this,key)
}


func staticKey_oracleAddress() = "static_oracleAddress"
func staticKey_extraFee() = "static_extraFee"
func staticKey_feeAggregator() = "static_feeAggregator"
func staticKey_turtleIncubator()= "static_turtleIncubatorAddress"
func staticKey_turtleIncubationFee()= "static_turtleIncubationFee"
func staticKey_eggAssetId() = "static_eggAssetId"
func staticKey_refContractAddress() = "static_refContractAddress"

func getOracle() = Address(tryGetString(staticKey_oracleAddress()).fromBase58String())
func getFeeAggregator() = Address(tryGetStringExternal(getOracle(),staticKey_feeAggregator()).fromBase58String())
func getEggAssetId() = tryGetStringExternal(getOracle(),staticKey_eggAssetId()).fromBase58String() #base58'C1iWsKGqLwjHUndiQ7iXpdmPum9PeCDFfyXBdJJosDRS'
func getTurtleIncubator() = Address(tryGetStringExternal(getOracle(),staticKey_turtleIncubator()).fromBase58String())
func getTurtleIncubationFee() = getIntegerValue(getOracle(),staticKey_turtleIncubationFee())
func getBurnAddress() = Address(tryGetStringExternal(getOracle(),"static_burnAddress").fromBase58String())
func getRefContractAddress() =  Address(tryGetStringExternal(getOracle(),staticKey_refContractAddress()).fromBase58String()) #base58'3P8ejTkfRpz9WqCwCuihesNXU5k3zmFFfVe'

func checkAdditionalPayment(payment: AttachedPayment)= {
  if isDefined(payment.assetId) then throw("BDCAP: Please attach waves") else
  let feeAmount = getIntegerValue(getOracle(),staticKey_extraFee())
  if payment.amount != feeAmount then throw("BDCAP: Please attach exactly "+feeAmount.toString()+" amount of wavelets") else
  [ScriptTransfer(getFeeAggregator(), feeAmount, unit)]

}

let delayForHatching = 2

let HatchingStarted = "BREEDING_STARTED"
let HatchingFinished = "BREEDING_FINISHED"

let GENS = ["A", "B", "C", "D", "E"]
let MaxGeneration = 7
let Generations = ["G", "H", "I", "K", "L", "M", "N", "O"]

func asBoolean(value: Any) = match value {
  case boolean: Boolean => boolean
  case _ => throw("TI: wrong type, expected: Boolean")
}

func getProcessStatusKey(address: String, txId: ByteVector) = {
  # stores the fact of hatching, 
  # if key exists = hatching started 
  # if false = not claimed
  # if true = claimed and nft id will be in the key of getDuckIdKey
  address + "_" + txId.toBase58String() +  "_status"
}

func getProcessFinishHeightKey(address: String, txId: ByteVector) = {
  address + "_" + txId.toBase58String() +  "_fh"
}

func getTRTLParentKey(txId: ByteVector, parentNum: Int) = {
  "inittx_" + txId.toBase58String() + "_parent" + parentNum.toString()
}

func getTRTLIdKey(address: String, txId: ByteVector) = {
  address + "_" + txId.toBase58String() +  "_di"
}

func getTRTLStatsKey(genString: String) = {
  "stats_" + genString + "_amount"
}

func tryGetInteger(key: String) = {
    let val = match getInteger(this, key) {
        case b:Int => b
        case _ => 0
    }
    val
}

func determineGeneration(g1: String, g2: String) = {
  let gIndex = max([Generations.indexOf(g1).value(), Generations.indexOf(g2).value()])
  Generations[gIndex + 1]
}

func getRandomNumber(variants: Int, txId: ByteVector, hatchingFinishHeight: Int, offset: Int) = {
  let randomSeedBlock = blockInfoByHeight(hatchingFinishHeight-1).value()
  let randomHash = sha256_16Kb(txId + randomSeedBlock.vrf.value())
  randomHash.toInt(offset) % variants
}

func getRandomGen(gen1: String, gen2: String, step: Int, txId: ByteVector, hatchingFinishHeight: Int) = {
  let randomNum = getRandomNumber(2, txId, hatchingFinishHeight, step)
  let gen = if (randomNum == 0) then gen1 else gen2
  gen
}

func generateTRTL(txId: ByteVector, hatchingFinishHeight: Int) = {
  let colorRandom = getRandomNumber(4, txId, hatchingFinishHeight, 11)
  let color = if (colorRandom == 0) then "R"
    else if (colorRandom == 1) then "D" 
    else if (colorRandom == 2) then "S" 
    else "G"

  let parent1Id = tryGetString(getTRTLParentKey(txId, 1)).fromBase58String()
  let parent2Id = tryGetString(getTRTLParentKey(txId, 2)).fromBase58String()

  let parent1Gen = assetInfo(parent1Id).value().name.split("")
  let parent2Gen = assetInfo(parent2Id).value().name.split("")

  let gen = "TRTL-" 
            + getRandomGen(parent1Gen[5], parent2Gen[5], 0, txId, hatchingFinishHeight)
            + getRandomGen(parent1Gen[6], parent2Gen[6], 1, txId, hatchingFinishHeight)
            + getRandomGen(parent1Gen[7], parent2Gen[7], 2, txId, hatchingFinishHeight)
            + getRandomGen(parent1Gen[8], parent2Gen[8], 3, txId, hatchingFinishHeight)
            + getRandomGen(parent1Gen[9], parent2Gen[9], 4, txId, hatchingFinishHeight)
            + getRandomGen(parent1Gen[10], parent2Gen[10], 5, txId, hatchingFinishHeight)
            + getRandomGen(parent1Gen[11], parent2Gen[11], 6, txId, hatchingFinishHeight)
            + getRandomGen(parent1Gen[12], parent2Gen[12], 7, txId, hatchingFinishHeight)
            + "-" + determineGeneration(parent1Gen[14], parent2Gen[14]) + color
    gen
}

func checkAssetInIncubator(assetId: String) = {
  let result = match getString(getTurtleIncubator(), assetId) {
    case b:String => true
    case _ => false
  }
  let result2 = match getString(this, assetId) {
    case c:String => true
    case _ => false
  }
  result || result2
}

func canBeBreeded(asset1Id: ByteVector, asset2Id: ByteVector) = {
  let asset1Gen = assetInfo(asset1Id).value().name.split("")
  let asset2Gen = assetInfo(asset2Id).value().name.split("")
  
  if (Generations.indexOf(asset1Gen[14]).value() >= MaxGeneration) then {
    "you can only breed turtles with generation " + Generations[MaxGeneration] + " and lower"
  } else {
    if (Generations.indexOf(asset2Gen[14]).value() >= MaxGeneration) then {
      "you can only breed turtles with generation " + Generations[MaxGeneration] + " and lower"
    } else {
      "true"
    }
  }
}

func isSymbolA(accumulated: Int, sym: String) = { if (sym == "A") then {accumulated+1} else {accumulated+0} }
func isSymbolB(accumulated: Int, sym: String) = { if (sym == "B") then {accumulated+1} else {accumulated+0} }
func isSymbolC(accumulated: Int, sym: String) = { if (sym == "C") then {accumulated+1} else {accumulated+0} }
func isSymbolD(accumulated: Int, sym: String) = { if (sym == "D") then {accumulated+1} else {accumulated+0} }
func isSymbolE(accumulated: Int, sym: String) = { if (sym == "E") then {accumulated+1} else {accumulated+0} }

func getAmountOrClear(amount: String) = {
  if (amount.contains("0")) then {""} else {amount}
}

func getGen(assetName: String) = {
  # getGen("TRTL-ABCDDCBA-GY") -> 2A2B2C2D-G
  let genotype = assetName.drop(5).dropRight(3).split("")
  let generation = assetName.split("")[14]
  getAmountOrClear(toString(FOLD<8>(genotype, 0, isSymbolA)) + "A") + getAmountOrClear(toString(FOLD<8>(genotype, 0, isSymbolB)) + "B") + getAmountOrClear(toString(FOLD<8>(genotype, 0, isSymbolC)) + "C") +
  getAmountOrClear(toString(FOLD<8>(genotype, 0, isSymbolD)) + "D") + getAmountOrClear(toString(FOLD<8>(genotype, 0, isSymbolE)) + "E")
}

@Callable(i)
func startTRTLBreeding(refererAddress: String) = {
  #TODO: add validation that feeValidate payment is added!
  if size(i.payments) != 4 then throw("Wrong amount of payments!") else
  strict feeValidate = checkAdditionalPayment(i.payments[0])
  let firstPayment = i.payments[1].value()
  let secondPayment = i.payments[2].value()
  let thirdPayment = i.payments[3].value()

  let leftToPay = getTurtleIncubationFee()
  strict payment = if leftToPay!=0 then {

    if (thirdPayment.assetId != getEggAssetId()) then throw("TB: You can attach only EGG tokens with the following asset id: " + getEggAssetId().toBase58String()) else 
    if (thirdPayment.amount != leftToPay) then throw("TB: To breed a Turtle you following amount of EGGlets: " + leftToPay.toString()+ " "+i.caller.toString()) else
    let refererRewardForbeach = fraction(leftToPay, 5, 100)
    strict refCall = invoke(getRefContractAddress(), "refPayment",
        [refererAddress], [AttachedPayment(getEggAssetId(), refererRewardForbeach)]).asBoolean()

    let toBurn = if refCall then leftToPay-refererRewardForbeach else leftToPay

    strict burnCall = invoke(getBurnAddress(), "burnAttachedPayments", [],[AttachedPayment(getEggAssetId(),toBurn)])
        
    leftToPay
    }else 0

  let firstAssetId = firstPayment.assetId.value().toBase58String()
  let secondAssetId = secondPayment.assetId.value().toBase58String()

  if (firstPayment.amount != 1 || secondPayment.amount != 1) then throw("NFT is not attached") else
  if (!(checkAssetInIncubator(firstAssetId) && checkAssetInIncubator(secondAssetId))) then {
    throw("You need to attach 2 valid NFT-turtles.")
  } else {
    if (tryGetInteger("asset_" + firstAssetId + "_children") > 0 || tryGetInteger("asset_" + secondAssetId + "_children") > 0) then {
      throw("One turtle can be used for breeding only once in its lifetime.")
    } else {
      let breededStatus = canBeBreeded(firstAssetId.fromBase58String(), secondAssetId.fromBase58String())
      if (breededStatus != "true") then {
        throw("These turtles cannot be breeded: " + breededStatus)
      } else {
        [
          StringEntry(getTRTLParentKey(i.transactionId, 1), firstAssetId),
          StringEntry(getTRTLParentKey(i.transactionId, 2), secondAssetId),
          StringEntry(getProcessStatusKey(i.caller.toString(), i.transactionId), HatchingStarted),
          IntegerEntry(getProcessFinishHeightKey(i.caller.toString(), i.transactionId), height + delayForHatching)
        ]++feeValidate
      }
    }
  }
}


@Callable(i)
func finishTRTLHatching(txIdStr: String) = {
  if size(i.payments) != 1 then throw("Wrong amount of payments!") else
  strict feeValidate = checkAdditionalPayment(i.payments[0])
  let txId = fromBase58String(txIdStr)
  let processStatusKey = getProcessStatusKey(i.caller.toString(), txId)
  let finishHeightKey = getProcessFinishHeightKey(i.caller.toString(), txId)
  let TRTLIdKey = getTRTLIdKey(i.caller.toString(), txId)

  let processTxStatus = getStringValue(this, processStatusKey)
  let processFinishHeight = getIntegerValue(this, finishHeightKey)
  
  if (processTxStatus == HatchingFinished) then {
    let TRTLId = getStringValue(this, getTRTLIdKey(i.caller.toString(), txId))
    throw("The turtle was breeded and claimed already, here is the folowing: " + TRTLId)
  } else 
    if (processFinishHeight > height) then throw("Breeding is not finished yet " + (processFinishHeight - height).toString() + " blocks remaining, it will take around " +  (processFinishHeight - height).toString() + " minutes")
    else {
      let TRTLGen = generateTRTL(txId, processFinishHeight)
      let farmGen = getGen(TRTLGen)
      let description = "{\"genotype\": \"" + TRTLGen + "\", \"crossbreeding\": true}"

      let asset = Issue(TRTLGen, description, 1, 0, false, unit, processFinishHeight)
      let assetId = asset.calculateAssetId()

      let amount = tryGetInteger(getTRTLStatsKey(TRTLGen)) + 1
      let quantity = tryGetInteger("stats_"+farmGen+"_quantity")
      let parent1Id = tryGetString(getTRTLParentKey(txId, 1))
      let parent2Id = tryGetString(getTRTLParentKey(txId, 2))

      [
        StringEntry(processStatusKey, HatchingFinished),
        StringEntry(TRTLIdKey, assetId.toBase58String()),
        StringEntry(assetId.toBase58String(), TRTLIdKey),
        IntegerEntry(getTRTLStatsKey(TRTLGen), amount),
        IntegerEntry("stats_"+farmGen+"_quantity", quantity+1),
        IntegerEntry("asset_" + parent1Id + "_children", 1),
        IntegerEntry("asset_" + parent2Id + "_children", 1),
        asset,
        ScriptTransfer(i.caller, 1, assetId),
        ScriptTransfer(i.caller, 1, parent1Id.fromBase58String()),
        ScriptTransfer(i.caller, 1, parent2Id.fromBase58String())
      ]++feeValidate
    }
}

@Callable(i)
func configureOracle(oracle: String) = {
  if i.caller != this then throw("ICU: admin only") else 
  [
    StringEntry("static_oracleAddress",oracle)
  ]

}

@Verifier(tx)
func verify() = sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey)